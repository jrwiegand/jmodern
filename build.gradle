apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'

mainClassName = 'jmodern.Main'

repositories {
    mavenCentral()
}

configurations {
    markdownDoclet
    quasar
}


dependencies {
    compile 'com.google.guava:guava:17.0'

    testCompile 'junit:junit:4.11' // A dependency for a test framework.

    markdownDoclet 'ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1'

    compile "co.paralleluniverse:quasar-actors:0.6.2"
    compile "co.paralleluniverse:quasar-core:0.6.2:jdk8"
    quasar "co.paralleluniverse:quasar-core:0.6.2:jdk8"
}

javadoc.options {
    docletpath = configurations.markdownDoclet.files.asType(List) // gradle should relly make this simpler
    doclet = "ch.raffael.doclets.pegdown.PegdownDoclet"
    addStringOption("parse-timeout", "10")
}


run {
    systemProperty 'jmodern.name', 'Josh'
    jvmArgs "-javaagent:${configurations.quasar.iterator().next()}" // gradle should make this simpler, too
    systemProperty "co.paralleluniverse.actors.moduleDir", "${rootProject.projectDir}/modules"
}
